apply plugin: 'com.android.application'

android {
    compileSdkVersion 28


    defaultConfig {
        applicationId "com.yfkk.cardbag"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias 'bonvo'
            keyPassword 'bonvo#999'
            storeFile file('C:/Android/key/bw.jks')
            storePassword 'bonvo#999'
        }
    }

    buildTypes {
        // debug和正式环境下的参数配置,编译后会在BuidConfig显示
        debug {
            minifyEnabled false
            buildConfigField("String", "INTERFACE", DEBUG_INTERFACE_HOST)
            buildConfigField("boolean", "IS_SHOW_LOG", DEBUG_SHOW_LOG)
            signingConfig signingConfigs.config
        }
        // 正式环境
        release {
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件,必须和minifyEnabled 一起使用
            shrinkResources true
            // 启用混淆
            minifyEnabled true
            buildConfigField("String", "INTERFACE", NORMAL_INTERFACE_HOST)
            buildConfigField("boolean", "IS_SHOW_LOG", NORMAL_SHOW_LOG)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 多渠道打包
    flavorDimensions "default"
    productFlavors {
        // 测试版渠道
        Channel_dev {}
        // 正式版渠道
        channel_main {} // 官方渠道
        channel_yingyongbao {}
        channel_baidu {}
        channel_xiaomi {}
        channel_huawei {}
        channel_oppo {}
        channel_vivo {}
        channel_sanxing {}
        channel_wandoujia {}
        channel_toutiao {}
        channel_guangdiantong {}
        channel_360 {}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    // 自定义APK名称,多渠道打包
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            variant.productFlavors.each { flavor ->
                def project = "yfkb"
                def versionName = variant.versionName
                def versionCode = variant.versionCode
                def port = "port"
                def apkName = project + "_" + versionName + "_" + versionCode + "_" + port + ".apk"

                output.outputFileName = apkName
            }
        }
    }
    // java8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    // 最新版都可以在这个网站去搜索 http://search.maven.org/
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // ButterKnife https://github.com/JakeWharton/butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // Rxandroid https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    // Retrofit https://github.com/square/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    // okhttp日志拦截器LoggingInterceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    // Rxpermissions 权限申请 https://github.com/tbruyelle/RxPermissions
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    // Picasso 图片加载框架 http://square.github.io/picasso/
    implementation 'com.squareup.picasso:picasso:2.71828'
    // 图片裁剪 https://github.com/ArthurHub/Android-Image-Cropper （Update to sdk v27 ,最新版需要使用androidX）
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    // 大图浏览 https://github.com/davemorrissey/subsampling-scale-image-view (不兼容v28)
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    // 百分比布局 https://github.com/hongyangAndroid/android-percent-support-extend
    implementation 'com.zhy:percent-support-extends:1.1.1'
    // SwipeBackLayout https://github.com/ikew0ng/SwipeBackLayout
    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'
    // 和js交互的WebView https://github.com/wendux/DSBridge-Android/blob/master/readme-chs.md
    implementation 'com.github.wendux:DSBridge-Android:3.0-SNAPSHOT'
    // 扫描二维码 https://github.com/yuzhiqiang1993/zxing
    implementation 'com.github.yuzhiqiang1993:zxing:2.2.5'
    // 解决方法数量65535上线超过
    implementation 'com.android.support:multidex:1.0.1'
}
